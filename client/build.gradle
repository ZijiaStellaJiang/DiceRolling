plugins {
	id 'org.springframework.boot' version '2.6.6'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'antlr'
}

group = 'edu.duke.summer'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web:2.6.7'
	testImplementation 'org.springframework.boot:spring-boot-starter-test:2.6.7'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.6.7'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf:2.6.7'
	annotationProcessor 'org.postgresql:postgresql:42.3.4'
	// https://mvnrepository.com/artifact/org.postgresql/postgresql
	implementation("org.postgresql:postgresql:42.3.5")

	// https://mvnrepository.com/artifact/javax.validation/validation-api
	implementation 'javax.validation:validation-api:2.0.1.Final'
	// https://mvnrepository.com/artifact/org.springframework.security/spring-security-core
	implementation 'org.springframework.security:spring-security-core:5.6.2'
	// https://mvnrepository.com/artifact/com.maxmind.geoip2/geoip2
	implementation 'com.maxmind.geoip2:geoip2:3.0.1'
	antlr "org.antlr:antlr4:4.5"

	//Websocket
	implementation 'org.webjars:webjars-locator-core'
	implementation 'org.webjars:sockjs-client:1.0.2'
	implementation 'org.webjars:stomp-websocket:2.3.3'
	implementation 'org.webjars:bootstrap:3.3.7'
	implementation 'org.webjars:jquery:3.1.1-1'
// https://mvnrepository.com/artifact/org.springframework/spring-messaging
	implementation 'org.springframework:spring-messaging:5.3.20'
// https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-websocket
	implementation 'org.springframework.boot:spring-boot-starter-websocket:2.6.7'

	//WebSecurity
	// https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-security
	implementation 'org.springframework.boot:spring-boot-starter-security:2.7.0'

}

tasks.named('test') {
	useJUnitPlatform()
}


test {
	testLogging {
		showStandardStreams = true
		exceptionFormat = 'full'
	}
}


// generate java from the ANTLR g4 files
generateGrammarSource {
	arguments = ['-visitor', '-package', 'edu.duke.summer.client.algorithm']
	outputDirectory = file("${project.buildDir}/generated-src/main/java/")
}

// copy the java code generated by ANTLR into a folder outside the build directory
// for the benefit of IntelliJ IDEA - make sure gen/main/java is marked as a generated
// sources folder
generateGrammarSource  {
	doLast {
		copy {
			from "${project.buildDir}/generated-src/main/java/"
			into "src/generated/java"
		}
	}
}

// make sure that ANTLR generates java before compiling the project
// include the generated java code in the compilation
compileJava.dependsOn generateGrammarSource
compileJava {
	source = ['build/generated-src/main/java', 'src/main/java/edu/duke/summer/client']
}

// when cleaning, remove the generated sources too
task removeGeneratedSource(type: Delete) {
	delete 'src/generated/'
}
clean.dependsOn removeGeneratedSource

// mark the generated source as such for IntelliJ IDEA
sourceSets.main.java.srcDirs += file("src/generated/java")
